apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'maven-publish'

ext {
    version_swagger_parser = '2.1.22'
    version_swagger_request_validator = '2.46.0'
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
}

dependencies {
    implementation project(':core:imposter-engine')
    implementation project(':core:config-resolver-s3')

    implementation "io.swagger.parser.v3:swagger-parser-v3:$version_swagger_parser"
    implementation "com.atlassian.oai:swagger-request-validator-core:$version_swagger_request_validator"
    implementation 'jakarta.activation:jakarta.activation-api:2.1.3'

    implementation platform("com.amazonaws:aws-java-sdk-bom:$version_aws_sdk")
    implementation 'com.amazonaws:aws-java-sdk-s3'

    // test
    testImplementation project(':test:test-utils')
    testImplementation project(':test:api-tests')
    testImplementation "org.testcontainers:testcontainers:$version_testcontainers"
    testImplementation "org.testcontainers:testcontainers-localstack:$version_testcontainers"
    testImplementation "org.mockito:mockito-core:$version_mockito"
    testImplementation "org.mockito.kotlin:mockito-kotlin:$version_mockito_kotlin"

    // java 11 compatibility
    testImplementation ("javax.xml.bind:jaxb-api:$version_jaxb_api")
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar

            repositories {
                maven {
                    url = version.endsWith('SNAPSHOT') ? mavenSnapshotRepository : mavenReleaseRepository
                    credentials(AwsCredentials) {
                        accessKey awsAccessKey
                        secretKey awsSecretKey
                    }
                }
            }
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }
}
