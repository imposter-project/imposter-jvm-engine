apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'com.gradleup.shadow'

compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
}

dependencies {
    implementation project (':imposter-cmd')
    implementation project (':distro:distro-base')

    // scripting
    implementation project (':scripting:scripting-graalvm')

    // plugins
    implementation project (':core:plugin-detector')
    implementation project (':mock:mock-openapi')
    implementation project (':mock:mock-rest')
    implementation project (':mock:mock-sfdc')
    implementation project (':mock:mock-soap')
    implementation project (':mock:mock-wiremock')

    // HBase excluded in favour of standalone plugin, due to https://github.com/outofcoffee/imposter/issues/547
    // implementation project (':mock:mock-hbase')

    // stores
    implementation project (':store:store-dynamodb')
    implementation project (':store:store-redis')
    implementation project (':store:store-graphql')

    // config resolvers
    implementation project (':core:config-resolver-s3')

    // libraries
    implementation project (':lib:fake-data')
}

mainClassName = 'io.gatehill.imposter.cmd.ImposterLauncher'

jar {
    manifest {
        attributes 'Main-Class': mainClassName

        // duplicates attribute from core so it is present in the merged jar
        attributes 'Imposter-Version': project.version

        // workaround for 'getCallerClass' warning due to log4j repackaging
        // see https://stackoverflow.com/questions/52953483/logmanager-getlogger-is-unable-to-determine-class-name-on-java-11
        attributes 'Multi-Release': 'true'
    }
}

application {
    applicationName = 'imposter'
}

shadowJar {
    archiveBaseName = 'imposter-all'
    archiveVersion = ''
    archiveClassifier = ''

    // all distro has more than 65535 entries
    zip64 true

    append 'META-INF/imposter.properties'
    append 'META-INF/plugin.properties'
    append 'META-INF/config-resolver.properties'

    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

task dist {
    dependsOn installDist
}
